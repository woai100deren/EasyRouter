if (isModule) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

android {
    compileSdkVersion Integer.parseInt(COMPILE_SDK_VERSION)
    buildToolsVersion BUILDTOOLS_VERSION

    resourcePrefix "${project.name}_" //给 Module 内的资源名增加前缀, 避免资源名冲突

    defaultConfig {
        if (!isModule) {
            applicationId "com.dj.easyrouter.businessmodule2"
        }
        minSdkVersion Integer.parseInt(MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.name]
            }
        }

        sourceSets {
            main {
                if (isModule) {
                    manifest.srcFile 'src/main/AndroidManifest.xml'
                } else {
                    manifest.srcFile 'src/main/module/AndroidManifest.xml'
                    java.srcDirs 'src/main/module/java', 'src/main/java'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation "com.android.support:support-v4:${SUPPORT_LIB_VERSION}"
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"

//    implementation project(":easyRouter-core")
//    annotationProcessor project(":easyRouter-compiler")

    implementation 'com.github.woai100deren:EasyRouter:1.0.0'
    annotationProcessor 'com.github.woai100deren:EasyRouter:1.0.0'

    implementation project(":module-java-export")
}